buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        // Gradle plugins
        gradlePluginVersion = '3.5.3'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
    }
}

repositories {
    google()
    jcenter()
}

group = 'com.urbanairship.unitysupport'
version = airshipProperties.version
description = "Urban Airship Android Unity Plugin"

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion airshipProperties.androidCompileSdkVersion.toInteger()

    defaultConfig {
        minSdkVersion airshipProperties.androidMinSdkVersion.toInteger()
        targetSdkVersion airshipProperties.androidTargetSdkVersion.toInteger()

        buildConfigField "String", "PLUGIN_VERSION", "\"${airshipProperties.version}\""
    }
}

dependencies {
    compileOnly files('libs/unity-classes.jar')
    implementation "androidx.annotation:annotation:$airshipProperties.androidxAnnotationVersion"
    implementation "com.urbanairship.android:urbanairship-fcm:${airshipProperties.androidAirshipVersion}"
    implementation "com.urbanairship.android:urbanairship-message-center:${airshipProperties.androidAirshipVersion}"
    implementation "com.urbanairship.android:urbanairship-automation:${airshipProperties.androidAirshipVersion}"
    implementation "com.urbanairship.android:urbanairship-location:${airshipProperties.androidAirshipVersion}"
}

task copyUnityClassesJar(type: Copy) {
  from file(getUnityClassesPath()).getParent()
  include file(getUnityClassesPath()).getName()
  rename ("classes.jar", "unity-classes.jar")
  into 'libs'
}

preBuild.dependsOn copyUnityClassesJar
publish.dependsOn assemble

def getUnityClassesPath() {
    def path = System.getProperty("UNITY_CLASSES_JAR")
    if (path == null || path.isEmpty()) {
        path = System.getenv("UNITY_CLASSES_JAR")
    }

    if (path == null || path.isEmpty() || !file(path).exists()) {
        path = "/Applications/Unity/Hub/Editor/$airshipProperties.unityVersion/PlaybackEngines/AndroidPlayer/Variations/mono/Release/Classes/classes.jar"
    }

    return path
}